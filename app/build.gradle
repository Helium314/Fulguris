apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'jacoco'
apply plugin: 'com.github.ben-manes.versions'
// Firebase
// Google Services Gradle plugin
apply plugin: 'com.google.gms.google-services'
// Apply the Crashlytics Gradle plugin
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion project.targetSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        vectorDrawables.useSupportLibrary = true
    }

    dexOptions {
        javaMaxHeapSize "8g"
    }

    buildTypes {
        debug {
            multiDexEnabled true
            minifyEnabled false
            shrinkResources false
            proguardFiles 'proguard-project.txt'
            testCoverageEnabled true
            applicationIdSuffix ".debug"
        }

        release {
            debuggable false
            multiDexEnabled true
            // Minify breaks settings since we migrated them to androidx
            minifyEnabled false
            shrinkResources false
            proguardFiles 'proguard-project.txt'
            testCoverageEnabled false
            // Use debug signing for now
            //signingConfig debug.signingConfig

            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a", "armeabi", "mips"
            }
        }
    }

    testOptions {
        unitTests.all {
            jacoco {
                // This option needs to be included for robolectric tests
                includeNoLocationClasses = true
            }
        }
    }

    flavorDimensions "brand", "version", "publisher"

    productFlavors {

        lightning {
            dimension "brand"
            applicationId "acr.browser.lightning"
            versionCode 101
            versionName "5.1.0"
        }

        slions {
            dimension "brand"
            applicationId "net.slions.fulguris"
            versionCode 65
            versionName "1.0.19"
            archivesBaseName = "Fulguris-v$versionName"
        }

        lite {
            dimension "version"
            buildConfigField "boolean", "FULL_VERSION", "Boolean.parseBoolean(\"false\")"
            applicationIdSuffix ".lite"
        }

        full {
            dimension "version"
            buildConfigField "boolean", "FULL_VERSION", "Boolean.parseBoolean(\"true\")"
            applicationIdSuffix ".full"
        }

        playstore {
            dimension "publisher"
            applicationIdSuffix ".playstore"
            buildConfigField "Sponsorship", "SPONSORSHIP", "Sponsorship.TIN"
        }

        download {
            dimension "publisher"
            applicationIdSuffix ".download"
            buildConfigField "Sponsorship", "SPONSORSHIP", "Sponsorship.BRONZE"
        }

        fdroid {
            dimension "publisher"
            applicationIdSuffix ".fdroid"
            buildConfigField "Sponsorship", "SPONSORSHIP", "Sponsorship.TIN"
        }

    }

    lintOptions {
        abortOnError true
    }

    packagingOptions {
        exclude '.readme'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Disabling this now breaks our runtime, go figure
    // Maybe we somehow used it before implicitly
    dataBinding {
        enabled true
    }
}

jacoco {
    toolVersion = '0.7.9' // See http://www.eclemma.org/jacoco/
}

dexcount {
    includeClasses = false
    includeFieldCount = false
    format = "tree"
    orderByMethodCount = true
    verbose = false
}

dependencies {
    // multidex debug
    debugImplementation 'androidx.multidex:multidex:2.0.1'

    // test dependencies
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.assertj:assertj-core:3.15.0'
    testImplementation 'org.mockito:mockito-core:3.2.4'
    testImplementation 'com.nhaarman:mockito-kotlin:1.6.0', {
        exclude group: 'org.jetbrains.kotlin'
    }
    testImplementation 'org.robolectric:robolectric:4.3.1'

    // support libraries
    implementation "com.google.android.material:material:1.2.1"
    implementation "androidx.palette:palette-ktx:1.0.0"
    implementation "androidx.annotation:annotation:1.1.0"
    implementation "androidx.vectordrawable:vectordrawable-animated:1.1.0"
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.fragment:fragment-ktx:1.2.5"
    implementation "androidx.drawerlayout:drawerlayout:1.1.1"
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // Play Store billing
    //playstoreImplementation 'com.android.billingclient:billing:3.0.2'
    implementation 'com.android.billingclient:billing:3.0.2'

    // html parsing for reading mode
    implementation 'org.jsoup:jsoup:1.11.3'

    // file reading
    final def mezzanineVersion = '1.1.1'
    implementation "com.anthonycr.mezzanine:mezzanine:$mezzanineVersion"
    kapt "com.anthonycr.mezzanine:mezzanine-compiler:$mezzanineVersion"

    // dependency injection
    final def daggerVersion = '2.27'
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    // view binding
    final def butterKnifeVersion = '10.2.1'
    implementation "com.jakewharton:butterknife:$butterKnifeVersion"
    kapt "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

    // permissions
    implementation 'com.anthonycr.grant:permissions:1.1.2'

    // proxy support
    implementation 'net.i2p.android:client:0.9.42'
    implementation 'net.i2p.android:helper:0.9.5'

    implementation 'com.squareup.okhttp3:okhttp:3.12.3'

    // rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.17'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    // tor proxy
    final def netCipherVersion = '2.0.0-alpha1'
    implementation "info.guardianproject.netcipher:netcipher:$netCipherVersion"
    implementation "info.guardianproject.netcipher:netcipher-webkit:$netCipherVersion"

    // memory leak analysis
    final def leakCanaryVersion = '1.6.3'
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    // Firebase
    // Recommended: Add the Firebase SDK for Google Analytics.
    implementation 'com.google.firebase:firebase-analytics:18.0.0'
    // Add the Firebase SDK for Crashlytics.
    implementation 'com.google.firebase:firebase-crashlytics:17.3.0'

    // Volley HTTP request
    implementation 'com.android.volley:volley:1.1.1'

    // Since java.time need API 26 we use that backport instead
    // See: https://github.com/JakeWharton/ThreeTenABP
    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.0'
}

kapt {
    arguments {
        arg("mezzanine.projectPath", project.rootDir)
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = '1.8'
        kotlinOptions {
            freeCompilerArgs += ['-XXLanguage:+InlineClasses']
            freeCompilerArgs += ['-progressive']
        }
    }
}
